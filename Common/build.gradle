apply plugin: libs.plugins.quilt.loom.get().pluginId

base {
    archivesName = "${mod_id}-common-${libs.versions.minecraft.get()}"
}

loom {
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

sourceSets.main.resources.srcDirs += [file('src/main/generated')]

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }

    //DO NOT USE IN PROJECT
    //Provides necessary stuff for project to compile
    modCompileOnly libs.quilt.loader
    compileOnly libs.groovyduvet.core

    modCompileOnly libs.cgl.common
}

import net.fabricmc.loom.task.AbstractRemapJarTask
tasks.withType(AbstractRemapJarTask).each {
    it.targetNamespace = "named"
}

groovydoc {
    exclude 'mods.groovy'
    use = true
    docTitle = "${mod_name} v${version}"
}

tasks.register('groovydocJar', Jar) {
    it.archiveClassifier = 'javadoc'
    it.from groovydoc.destinationDir
    it.dependsOn(groovydoc)
}

processResources {
    exclude 'mods.groovy'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId base.archivesName.get()
            version = project.version
            from components.java
            artifact groovydocJar
        }
    }
}
